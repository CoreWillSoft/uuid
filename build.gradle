buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.21'
    }
}

repositories {
    jcenter()
    mavenCentral()
}
group 'com.benasher44'
version '0.0.1'

apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven-publish'

kotlin {
    targets {
        targetFromPreset(presets.jvm, 'jvm')
        targetFromPreset(presets.js, 'js')
        targetFromPreset(presets.macosX64, 'macos')
        targetFromPreset(presets.iosX64, 'iosSim')
        targetFromPreset(presets.iosArm64, 'iosDevice')
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }
        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }
        macosMain { kotlin.srcDirs += 'src/nativeMain/kotlin' }
        macosTest { kotlin.srcDirs += 'src/nativeTest/kotlin' }
        iosDeviceMain { kotlin.srcDirs += 'src/nativeMain/kotlin' }
        iosDeviceTest { kotlin.srcDirs += 'src/nativeTest/kotlin' }
        iosSimMain { kotlin.srcDirs += 'src/nativeMain/kotlin' }
        iosSimTest { kotlin.srcDirs += 'src/nativeTest/kotlin' }
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.30.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
